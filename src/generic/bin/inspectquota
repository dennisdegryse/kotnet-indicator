#!/usr/bin/perl -w
use strict;
use WWW::Mechanize;
use Fcntl ':flock';

open my $self, '<', $0 or die "Couldn't open self: $!";
flock $self, LOCK_EX | LOCK_NB or die "This script is already running";

if (@ARGV != 3)
{
    print "ERR 0 Incomplete configuration";
    exit 0;
}

my $username = $ARGV[0];
my $password = $ARGV[1];
my $filename = $ARGV[2];
my $client = WWW::Mechanize->new(agent => "gnome-shell-extension-kotnet 0.1");
my $result = '';

while (1) 
{
    $result = '';

    while (1)
    {
        # Try login
        $client->get("https://netlogin.kuleuven.be/cgi-bin/wayf.pl?inst=kuleuven&lang=en&submit=Ga+verder+%2F+Continue") or die;
        $client->form_name ("netlogin");
        $client->set_visible($username, $password) or die;
        $client->submit() or die;

        # Check result code
        $client->content =~ m/<rc=(\d*?) (.*?)( <.*)?>/i;

        last if ($1 == 100);

        if ($1 != 206) {
            $result = "ERR $1 $2";
            last;
        }

        # Logout stale session
	    $client->content =~ m/<INPUT type=hidden name="ip" value="([0-9\.]*)">/;

    	$client->get("https://netlogin.kuleuven.be/cgi-bin/wayf.pl?inout=logout&ip=$1&uid=kuleuven/$username&lang=en") or die;
    	$client->form_name ("netlogout");
    	$client->set_visible($password) or die;
    	$client->submit();
    }

    if ($result eq '') 
    {
        if ($client->content =~ m/<h2>campusnet<\/h2>/i)
        {
        	$result = "OK -1 -1";
        } 
        else
        {
            $client->content(format => 'text') =~ m/(\d+) MBytes.*?(\d+) MBytes/;
            $result = "OK $1 $2";
        }
    }

    open (FILE, ">$filename");
    print FILE $result;
    close (FILE);

    # Quit if there was no valid result
    last if ($result !~ /OK/);

    # Sleep for 5 minutes
    sleep 300;
}
